// Add dependency on the protobuf plugin
buildscript {
    repositories {
        maven {
            // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
        }
    }
    dependencies {
        classpath libraries.protobuf_plugin
    }
}

description = "gRPC: Services"

dependencies {
    compile project(':grpc-protobuf'),
            project(':grpc-stub')
    compile (libraries.instrumentation_api) {
        // prefer 2.0.19 from libraries instead of 2.0.11
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        // we'll always be more up-to-date
        exclude group: 'io.grpc', module: 'grpc-context'
    }
    testCompile project(':grpc-testing'),
            libraries.netty_epoll // for DomainSocketAddress
    signature "org.codehaus.mojo.signature:java16:1.1@signature"
}

configureProtoCompilation()

// Let intellij projects refer to generated code
idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
        testSourceDirs += file("${projectDir}/src/generated/test/java")
        testSourceDirs += file("${projectDir}/src/generated/test/grpc")
    }
}
